ant.importBuild '../build.xml'

tasks.whenTaskAdded { task ->
    if (task.name.equals("lint")) {
        task.enabled = false
    }
}

apply plugin: 'com.android.application'
apply from: 'jacoco.gradle'//gradlew testReleaseUnitTestCoverage
apply from: 'javadoc.gradle'//gradlew generateReleaseJavadoc

task cleanExtra(type: Delete) {
    delete "android-apk"
}

clean.dependsOn(cleanExtra)

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFileName = new File("android-apk/mgiandia-library-${variant.name}.apk")
        }
    }

    compileOptions {
        encoding "UTF-8"
    }

    signingConfigs {
        release {
            storeFile file("keystore.jks")
            storePassword "password"
            keyAlias "alias"
            keyPassword "password"
        }
    }

    defaultConfig {
        applicationId "com.mgiandia.library"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            testCoverageEnabled = true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

}

task movejunit {
    doLast {
        file("build/reports/tests/testReleaseUnitTest/").renameTo(file("../reports/junit/"))
    }
}
